buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

plugins {
    id "com.github.kt3k.coveralls" version "2.8.1"
 }

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "findbugs"
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

group 'com.treasuredata'
archivesBaseName = 'underwrap'
version = '0.1.9-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

def deployUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ''
def deployPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ''

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

findbugs {
    effort = 'max'
    excludeFilter = file("${rootProject.projectDir}/config/findbugs-exclude.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    compile 'com.google.guava:guava:22.0'
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'net.jodah:failsafe:1.0.4'
    compile 'org.jboss.resteasy:resteasy-undertow:3.1.3.Final'
    compile 'io.undertow:undertow-servlet:1.4.15.Final'
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs += ["-Duser.language=en"]
    options.compilerArgs = ['-Xlint:all']
}

task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            pom.project {
                name 'underwrap'
                description "A very thin wrapper of Undertow and Resteasy"
                packaging 'jar'
                url 'https://github.com/treasure-data/underwrap'
                scm {
                    connection 'scm:git:git://github.com/treasure-data/underwrap.git'
                    developerConnection 'scm:git@github.com:treasure-data/underwrap.git'
                    url 'https://github.com/treasure-data/underwrap'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'komamitsu'
                        name 'Mitsunori Komatsu'
                        email 'komamitsu@gmail.com'
                    }
                    developer {
                        id 'tagomoris'
                        name 'Satoshi Tagomori'
                        email 'tagomoris@gmail.com'
                    }
                }
            }
        }
    }
}
